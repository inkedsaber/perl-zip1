#!/usr/bin/env perl
use strict;
use warnings;

use Term::ANSIColor;
use Getopt::Long 'HelpMessage';

Getopt::Long::Configure ("bundling");

GetOptions(
    "d|dir=s"   => \ my $dir,
    "e|extract" => \ my $extract,
    "v|verbose" => \ my $verbose,
    "h|help"    => sub { HelpMessage(0) },
) or HelpMessage(1);

HelpMessage(1) unless $dir;

=head1 SYNOPSIS

  --dir,-d        Directory where '.zip1' files located (required)
  --extract,-e    Extract content from renamed files
  --verbose,-v    Display additional info
  --help,-h       Print this help

=cut

# For future use:
# use utf8;
# use Encode;

chdir($dir)
  or die colored("Error: can't chdir to $dir $!", 'red');

opendir my $dh, $dir
  or die colored("Error: couldn't open dir: $!\n", 'red');

foreach (readdir $dh) {
    next if /^\.+$/;
    if ((my $newname = $_) =~ s/zip1$/zip/) {

        print colored("OK: $_ -> $newname\n", 'bright_green');
        rename($_, $newname)
          or die colored("Error: can't rename: $!", 'red');

        if ($extract) {
            if ($verbose) {
                system('unzip', $newname) == 0
                  or die colored("Error: unzipping $newname failed: $?", 'red');
            } else {
                system('unzip', '-q', $newname) == 0
                  or die colored("Error: unzipping $newname failed: $?", 'red');
            }
            print colored("OK: $newname\'s content is extracted\n", 'bright_green');
        }

        unlink $newname;
    }
}
closedir $dh;

